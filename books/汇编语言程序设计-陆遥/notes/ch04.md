---
title: "第四章 8086汇编程序设计"
category:
 - Programming
tag:
 - Assembly
---

## 1. 顺序程序设计

按照源程序的指令次序顺序执行.

例 4.1: 计算一下表达式的值:

$X=\frac{A\times B+C-D}{E+F}$

```assembly
ASSUME CS:CSEG, DS:DSEG, SS:SSEG

SSEG SEGMENT STACK        ; 定义堆栈段, STACK 为组合类型(本程序似乎用不上...)
         DW 16 DUP(?)    ; 分配内存, 16 个字(32 字节)
SSEG ENDS

DSEG SEGMENT        ; 定义数据段
    ; 变量设置
    A    DB 56
    B    DB -20
    Z    DB 106     ; 此处不能使用 C 作为变量名
    D    DB 80
    E    DB -112
    F    DB 15
    X    DW ?       ; 为 X 分配一个字的空间
DSEG ENDS

CSEG SEGMENT               ; 代码段
    START:MOV  AX, DSEG    ; 程序入口, 将数据段 DSEG 地址给 AX
          MOV  DS, AX      ; 将 DSEG 存如 DS
          
    ; 计算 AxB 并将结果存在 BX 中
          MOV  AL, A
          IMUL B           ; A, B为有符号数
          MOV  BX, AX

    ; 计算 AxB+C-D, 将结果存在 BX 中
          MOV  AL, Z
          CBW              ; 扩展 C, 避免溢出
          ADD  BX, AX      ; AxB+C
          MOV  AL, D
          CBW
          SUB  BX, AX      ; AxB+C-D

    ; 计算 E+F, 结果存在 CX 中
          MOV  AL, E
          CBW
          MOV  CX, AX
          MOV  AL, F
          CBW
          ADD  CX, AX

    ; 计算最终结果
          MOV  AX, BX
          CWD              ; 扩展 AX 防止溢出
          IDIV CX
          MOV  X, AX       ; 将结果存至 X 中
          
    ; 结束程序
          MOV  AH, 4CH
          INT  21H
CSEG ENDS
END START
```

无法使用 C 作为变量名, 详见 https://stackoverflow.com/questions/11055487/letter-c-as-variable-name

在 DOS 中使用 DEBUG 查看 X 的值, DS:[0006] 的位置为 000B, 结果正确

![image-20240902210604453](https://raw.githubusercontent.com/dreamjz/pics/main/pics/2024/202409022106052.png)

## 2. 分支程序设计

### 例 4.2

判断字类型的变量 X 值为奇数还是偶数. 若为偶数 将变量 OE 置零, 否则置 1.

```assembly
assume cs:code, ds:data, ss:stk 

stk segment stack
        dw 16 dup (?)
stk ends

data segment
    x    dw 23
    oe   db ?
data ends

code segment
    start: mov  ax, data
           mov  ds, ax

           test x, 1        ; 计算 x&1
           jz   isEven      ; 判断 ZF=0? (结果是否为 0)
           mov  oe, 1
           jmp  ok

    isEven:mov  oe, 0

    ok:    mov  ah, 4ch
           int  21h
code ends
end start 
```

### 例 4.3

A, B, C 为无符号字节类型变量, 编写程序, 让其按照值升序排序.

```assembly
assume cs:code, ds:data, ss:stk  

stk segment stack
        dw 16 dup (?)
stk ends

data segment
    A    db 56
    B    db 24
    X    db 35
data ends

code segment
    start:mov  ax, data
          mov  ds, ax
          
          mov  al, A
          cmp  al, B       ; 比较 A, B
          jbe  A_X         ; A <= B 则比较 A,X
          xchg al, B
          mov  A, al
    
    A_X:  cmp  al, X
          jbe  B_X
          xchg al, X
          mov  A, al

    B_X:  mov  al, B
          cmp  al, X
          jbe  ok          ; B <= X 排序完成
          xchg al, X
          mov  B, al
          
    ok:   mov  ah, 4ch
          int  21h
         
code ends
end start
```

### 例 4.4 

字节变量 SCORE 存有课程的百分制成绩, 编写程序, 将其转换为 5 分制. 并将结果用 A~E 存入 LEVEL 变量中. (A: [90, 100], B: [80, 90), C: [70, 80), D: [60, 70), E: [0, 60))

```assembly
assume cs:code, ds:data, ss:stk 

stk segment stack
        dw 16 dup (?)
stk ends

data segment
    SCORE db 95
    LEVEL db ?
data ends

code segment
    start:mov ax, data
          mov ds, ax

          mov al, SCORE
          cmp al, 90
          jb  C80          ; < 90
          mov ah, 'A'
          jmp ok

    C80:  cmp al, 80
          jb  C70
          mov ah, 'B'
          jmp ok

    C70:  cmp al, 70
          jb  C60
          mov ah, 'C'
          jmp ok

    C60:  cmp al, 60
          jb  L_E
          mov ah, 'D'
          jmp ok

    L_E:  mov ah, 'E'
    
    ok:   mov LEVEL, ah
          mov ah, 4ch
          int 21h
code ends
end start
```

## 3. 循环程序设计

### 例 4.5

计算某课程的平均分和最高分.

```assembly
assume cs:code, ds:data, ss:stk 

stk segment stack
        dw 16 dup (?)
stk ends

data segment
    SCORES db  76,82,65,95,24,50,82,92
    N      EQU $-SCORES                   ; N 为 数据元素个数
    AVG    DB  ?
    BEST   DB  ?
data ends

code segment
    start:mov  ax, data
          mov  ds, ax
    
          lea  si, SCORES    ; 获取数组首地址
          xor  ax, ax        ; AX 清零
          mov  bl, [si]      ; bl 存储最高分, 初始为 SCORES[0]
          mov  cx, N         ; 循环计数
          
    cont: add  al, [si]      ; 累加分数
          adc  ah, 0
          cmp  bl, [si]
          jae  next          ; 更新最高分
          mov  bl, [si]

    next: inc  si
          loop cont
          mov  cl, N
          div  cl
          mov  AVG, al
          mov  BEST, bl
          
          mov  ah, 4ch
          int  21h
code ends
end start
```

### 例 4.6 

数组的前三个元素为 0, 0, 1, 从第四个元素起, 每个元素为前三个元素之和. 求出后续元素直到元素值大于 200 时结束.

```assembly
assume cs:code, ds:data, ss:stk 

stk segment stack
        dw 16 dup (?)
stk ends

data segment
    ARR  dw ?,0,0,1,100 dup (?)    ; 数组首位存放元素个数
data ends

code segment
    start:mov ax, data
          mov ds, ax

          lea si, ARR+4*2             ; 下个元素的地址
          mov cx, 3                   ; 初始话元素个数
    cont: cmp word ptr [si-2], 200    ; 比较尾部元素和 200
          ja  exit                    ; 超过则结束
    ; 累加前三个元素
          mov ax, [si-6]
          add ax, [si-4]
          add ax, [si-2]
          mov [si], ax                ; 追加至数组
          inc cx                      ; 更新元素数量
          add si, 2
          jmp short cont              ; 循环
          
    exit: mov arr, cx
          mov ah, 4ch
          int 21h
code ends
end start
```

### 例 4.7

检查无符号数组中是否存在水仙花数, 存在则将变量 ans 置 1, 否则为 0.

```assembly
assume cs:code, ds:data, ss:stk 

stk segment stack
        dw 16 dup (?)
stk ends

data segment
    ARR  dw  376,82,753,2659
    N    equ ($-ARR)/2
    ANS  db  ?
data ends

code segment
    start:mov  ax, data
          mov  ds, ax

          lea  si, ARR
          mov  cx, N
          mov  dh, 10

    cont: cmp  word ptr [si], 999
          ja   next
          cmp  word ptr [si], 100
          jb   next
          mov  ax, [si]
          
          div  dh
          mov  dl, ah
          xor  ah, ah
          
          div  dh
          mov  bx, ax
          xor  bp, bp
          mul  bl
          mul  bl
          add  bp, ax

          mov  al, bh
          mul  bh
          mul  bh
          add  bp, ax

          mov  al, dl
          mul  dl
          mul  dl
          add  bp, ax
          
          cmp  bp, [si]
          jmp  exit

    next: add  si, 2
          loop cont
          mov  ANS, 0
          jmp  short fin

    exit: mov  ANS, 1
    fin:  mov  ah, 4ch
          int  21h
code ends
end start
```

### 例 4.8

某班有 40 个学生, 平均分为 5 个小组, 求各小组的总分.

```assembly
assume cs:code, ds:data, ss:stk 

stk segment stack
        dw 16 dup (?)
stk ends

data segment
    ARR   db 1,2,3,4,5,6    ;...
    N     =  5
    N1    =  8
    TOTAL dw 5 dup (?)
data ends

code segment
    start:mov  ax, data
          mov  ds, ax
          lea  si, ARR
          lea  di, TOTAL
          mov  cx, N
    
    glop: xor  ax, ax
          push cx           ; 暂存外循环计数器
          mov  cx, N1       ; 开始内循环
    
    slop: add  al, [si]
          adc  ah, 0
          inc  si
          loop slop
          mov  [di], ax
          add  di, 2
          pop  cx
          loop glop
          mov  ah, 4ch
          int  21h
code ends
end start
```

### 例 4.9 冒泡排序

```assembly
assume cs:code, ds:data, ss:stk 

stk segment stack
        dw 16 dup (?)
stk ends

data segment
    Arr  db  17,22,9,80,37
    N    equ $-Arr
data ends

code segment
    start:mov  ax, data
          mov  ds, ax

          mov  cx, N-1
    rond: push cx
          lea  si, Arr
            
    ; 内层循环, 交换升序元素
    cont: mov  al, [si]
          cmp  al, [si+1]    ; 比较 a[i], a[i+1]
          jae  next
    ; 交换 a[i], a[i+1]
          xchg al, [si+1]
          mov  [si], al

    next: inc  si
          loop cont
          pop  cx
          loop rond
            
          mov  ah, 4ch
          int  21h
code ends
end start
```

## 4. 子程序设计

### 4.1 定义

```assembly
过程名 PROC [类型]
 ...
过程名 ENDP
```

类型:

- NEAR: 与调用程序在同一代码段内
- FAR: 不在同一代码段内

### 4.2 调用和返回

#### 1) 调用

```assembly
CALL 过程名 ; 近调用
CALL FAR PTR 过程名 ; 远调用
```

CALL 指令:

- 转移到子程序入口

- 保存返回点:
  将返回点地址入栈

  - 近调用: 只需记录 IP
  - 远调用: 记录 CS:IP

因为保存返回点地址需要使用堆栈, 所以在定义了子程序的源程序中, 必须定义堆栈.

#### 2) 返回

```assembly
RET [n]
```

可选项 n 是一个无符号数, 若添加了 n, 从堆栈弹出返回点后, 将栈顶指针修改为 `SP:IP <- SP+n` (即将 SP 向栈底移动 n 个字节).

注意: 使用 RET 时, 要保证返回点的地址处于栈顶位置.

### 4.3 保护和恢复现场

- 保护现场: 调用子程序时, 将相关寄存器数据进行保存, 推入栈中
- 恢复现场: 返回时, 恢复保存的数据

```
过程名 PROC [NEAR/FAR]
 [保护现场]
 过程体
 [恢复现场]
 RET
过程名 ENDP
```

### 4.4 子程序的参数传递

#### 1) 通过寄存器传递

- 直接将数据存储至指定的寄存器中
- 将数据的地址存入寄存器

不适合传递大量参数

#### 2) 通过内存变量传递

将数据存入内存变量中

#### 3) 通过堆栈传递

- 在子程序调用之前, 将数据存入堆栈. 
- 调用 CALL 后, 从堆栈取出数据 (非出栈)
- 返回后, 使用带 n 的 RET 指令, 将使用过的无效数据出栈

#### 例 4.10

将班级考试成绩按降序排序, 计算器及格率 (十进制百分比, 如: 80.52%)

```assembly
```

//TODO: 

#### 例 4.11

//TODO: 

### 4.5 子程序嵌套

子程序嵌套理论上无限制, 但嵌套深度实际上收到堆栈容量的限制, 若出现堆栈溢出则将导致程序运行错误.

#### 例 4.12

//TODO:

## 5. 输入和输出

### 5.1 软中断

```assembly
INT n
```

调用中断号为 n 的中断程序服务.

### 5.2 调用 DOS 功能程序实现输入输出

DOS 的 21H 中断号包含多种具体功能，调用步骤：

- 功能号： AH
- 子功能号：AL
- 设置参数
- 执行 INT 21H

#### 1) 带回显的单字符输入

功能号：01H

返回参数：AL

从键盘上接收一个字符，并回显。检测按键是否为 Ctrl+C / Ctrl+Break，若是则终止程序。

```assembly
mov ah, 01h
int 21h
```

#### 2) 单字符输出显示

功能号：02H

参数：DL

```assembly
mov ah, 02h
mov dl, 'a' ; 待输出的字符
int 21h
```

#### 例 4.13

// TODO：

#### 3) 不带回显的单字符输入

- 07H：返回参数 AL，不检测 Ctrl+C / Ctrl+Break
- 08H：返回参数 AL，检测  Ctrl+C / Ctrl+Break

#### 4) 字符串输出

09H:
	参数：DS：DX，字符串首地址

字符串必须以 ‘$’ 结尾。

#### 5）字符串输入

0AH

参数：DS:DX， 字符串缓冲区

```assembly
缓冲区名 DB n, ?, n DUP (?)
```

- n 字符串存储区最大字节数
- ? 字符串实际长度
- n DUP (?) 实际存储区

#### 例 4.4

// TODO

#### 6) 先清除键盘缓冲区后输入

功能号：0CH

参数：AL

## 6. 宏的使用

### 6.1 定义、调用和展开

#### 宏定义

宏指令遵循先定义后使用的原则
```assembly
宏指令名 MACRO [形式参数表]
...
ENDM
```

#### 宏调用

```assembly
宏指令名 [实际参数表]
```

#### 宏展开

汇编程序在对包含宏调用的源程序进行汇编时，会使用宏体的语句替换宏调用，被称为宏展开。

#### 例 4.15

// TODO

#### 例 4.16

// TODO

#### 例 4.17

// TODO

#### 例 4.18

// TODO

### 6.2 宏定义的指令标号

若宏定义中使用了指令标号，当宏被多次调用时，会出现标号重复定义错误。此时需要使用 LOCAL 定义局部标号。

```assembly
LOCAL 局部标号1,局部标号2,局部标号3,...
```

### 6.3 宏库的建立和使用

宏指令可以集中起来单独建立一个文件，以 MAC 为扩展名。

使用 INCLUDE 指令引入宏文件：

```assembly
INCLUDE 宏文件名
```

若宏文件中部分指令用不上，可以在 INCLUDE 后使用 PURGE 删除指定的指令。

```assembly
PURGE 宏指令表
```

